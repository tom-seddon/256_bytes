                .include "submodules/beeb/include/beeb.s65"
                .cpu '65c02'

;-------------------------------------------------------------------------

*=$70
                .dsection zp
*=$1900
                .dsection code

;-------------------------------------------------------------------------

                .section zp
addr:
                .fill 2
; xc:
;                 .fill 1
yc:
                .fill 1
counter:
                .fill 1
pixel:
                .fill 1
temp:
                .fill 1
temp_counter:
                .fill 1
                .endsection

;-------------------------------------------------------------------------

                .section code
start:
                ; best to start with counter set to 0, to ensure the
                ; first bit of music lasts for a decent amount of
                ; time.
                ;
                ; the initial value of addr isn't so important.
                stz counter

                ldx #init_text_end-init_text_begin
init_loop:
                lda init_text_begin-1,x
                jsr oswrch
                dex
                bne init_loop
                
                sei
                stz $fe20
                sta $fe21

                ; set some random frequency on tone 3
                ;
                ; (best to reuse tone 3, as it's the bootup beep)
                lda #$80
                jsr sn_write
                lsr a           ;A=$40
                jsr sn_write

fix_addr_msb:
                inc addr+1
                bpl got_addr_msb

                jsr next_frame

got_addr_msb:
                lda addr+1
                and #%00000011
                ora #$7c
                sta addr+1

do_pixel:                
                lda addr+1
                sta yc
                lda addr+0

                ; addr high nybble is always 7 (%0111)
                asl a
                rol yc          ;C=0
                asl a
                rol yc          ;C=1
                asl a
                rol yc          ;C=1

                lda counter
layer0:
                jsr type_codes+1*type_code_size
                and #4
                sta temp

                lda counter
                lsr a
                tax
                sec
layer1:
                jsr type_codes+2*type_code_size
                and #8
                ora temp
                lsr a
                sta temp

                txa
                lsr a
                sec
layer2:
                jsr type_codes+3*type_code_size
                and #8
                ora temp
                lsr a
                
pixel_mask=*+1
                and #0
                tay

                asl a
call_music:
                jsr music1
                and #$0f
                ora #%10010000  ;tone 3 attenuation
                jsr sn_write
                
                lda pixel_table,y
                sta (addr)

next_byte:
                inc addr+0
                beq fix_addr_msb

                bra do_pixel

next_frame:
                ; TODO - could use top 6 bits of address instead?

                lda acccon
                eor #acccon.d|acccon.x
                sta acccon

                inc counter
                bne +
                ldx #$18        ;CLC
                stx update_sequence
+
                lda counter
                bit #7
                bne done

update_sequence:
                sec
                rol pixel_mask

                lda call_music+1
                eor #<(music1^music2)
                sta call_music+1

                ; all are offset so that the last char in the
                ; init string can be a 3. this is then reused to set
                ; up the palette.

                ; next frame...
                ldx #layer0+1-(start+3)
                jsr change_layer

                ldx #layer1+1-(start+3)
                jsr change_layer

                ldx #layer2+1-(start+3)
change_layer:
                clc
                lda start+3,x   ;code LSB
                adc #type_code_size
                bcc +                 ;90 02
                lda #type_codes-start ;a9 e2
+
                sta start+3,x
music1:
                eor #$0f
                and counter
done:
                rts

init_text_begin=*-7          
                .byte 32
                .byte 1
                .byte 0
                .byte 23
sn_write:
                sta system_via.ora_no_handshake
                stz system_via.orb
                ; 6 remaining VDU 23 bytes above
                inx             ;e8        R12 value = $28 (6-bit reg)
                tsb $1700       ;0c 00 17  23,0,12 - set CRTC R12
                sta $16         ;85 16     22,135 - shadow mode 5
init_text_end:
                ldx #8
                stx system_via.orb
                rts

music2:
                ora counter
                rts
                
pixel_value: .function colour
right=(colour&8)<<3|(colour&4)<<2|(colour&2)<<1|(colour&1)<<0
                .endfunction (right<<1|right<<0)
                
pixel_table:
                .for i=0,i<8,i+=1
                .if (i&4)!=0
                .byte pixel_value(8) ;yellow
                .elsif (i&2)!=0
                .byte pixel_value(2) ;red
                .elsif (i&1)!=0
                .byte pixel_value(0) ;blue
                .else
                .byte pixel_value(1) ;black
                .endif
                .endfor
                
                .fill 256-(types_end-fill_end)-(*-start)
fill_end:
                
type_code_size=6
                
type_codes:                
type4: .block
                ; chequerboard scroll east
                sbc addr+0
                eor yc
                rts
                .byte 3         ;palette value - colour 0 = blue
                .endblock
                .cerror size(type4)!=type_code_size,''
type0: .block
                ; dots scroll west
                adc addr+0
                and yc
                asl a
                rts
                .endblock
                .cerror size(type0)!=type_code_size,''
               
type1: .block
                ; slash scroll east
                sbc addr+0
                sec
                sbc yc
                rts
                .endblock
                .cerror size(type1)!=type_code_size,''

type2: .block
                ; grid scroll north
                adc yc
                ora addr+0
                asl a
                rts
                .endblock
                .cerror size(type2)!=type_code_size,''

type3: .block
                ; backslash scroll west
                adc addr+0
                sec
                sbc yc
                rts
                .endblock
                .cerror size(type3)!=type_code_size,''
types_end:
                .endsection