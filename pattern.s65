                .include "submodules/beeb/include/beeb.s65"
                .cpu '65c02'

;-------------------------------------------------------------------------

double_buffer=true
                
; doesn't actually seem to work :(
vsync=false
                
;-------------------------------------------------------------------------

*=$70
                .dsection zp
*=$1900
                .dsection code

;-------------------------------------------------------------------------

                .section zp
addr:
                .fill 2
; xc:
;                 .fill 1
yc:
                .fill 1
counter:
                .fill 1
pixel:
                .fill 1
temp:
                .fill 1
temp_counter:
                .fill 1
                .endsection

;-------------------------------------------------------------------------

                .section code
start:
                lda #22
                jsr oswrch
                lda #12         ;i.e., mode 4
                jsr oswrch

                .logical *

                sei
                stz $fe20

                sta $fe00
                lda #$28        ;$7c00
                sta $fe01
                lda #1
                sta $fe00
                ldy #32
                sty $fe01

                .if double_buffer
                lda #acccon.d
                sta acccon
                .endif

                clc
                dec a
                tay
set_palette_loop:
                sta $fe21
                adc #$11
                bcc set_palette_loop

                ; doesn't matter what the initial values of addr or
                ; counter are, though the writing will start at an
                ; arbitrary position on screen and the counter may or
                ; may not increment first time round...
fix_addr_msb:
                inc addr+1
                bpl got_addr_msb

                lda counter
                inc a
                tax
                eor counter
                stx counter
                and #32
                beq swap_buffers

                ; next frame...
                ldx #layer0-start
                jsr change

                ldx #layer1-start
                jsr change

                ldx #layer2-start
                jsr change

swap_buffers:
                lda acccon
                eor #acccon.d|acccon.x
                sta acccon

got_addr_msb:
                lda addr+1
                and #%00000011
                ora #$7c
                sta addr+1
loop:
                
                lda addr+1
                sta yc
                lda addr+0
                ;sta xc

                asl a
                rol yc
                asl a
                rol yc
                asl a
                rol yc

                ; C=1 at this point - addr high nybble is always 7

                lda counter
                and #31
                tax
layer0:
                jsr type0
                and #4
                sta temp

                txa
                lsr a
                tax
layer1:
                jsr type1
                and #8
                ora temp
                lsr a
                sta temp

                txa
                lsr a
layer2:
                jsr type2
                and #8
                ora temp

                lsr a
                tax
                lda pixel_table,x
                
                sta (addr)

                inc addr+0
                beq fix_addr_msb
                bra loop

change:
                clc
                lda start+1,x   ;code LSB
                adc #8
                cmp #types_end-start
                bcc +
                lda #type0-start
+
                sta start+1,x
                rts

type0: .block
                sec
                sbc addr+0
                sec
                sbc yc
                rts
                nop
                .endblock
                .cerror size(type0)!=8,''

type1: .block
                clc
                adc addr+0
                sec
                sbc yc
                rts
                nop
                .endblock
                .cerror size(type1)!=8,''

type2: .block
                clc
                adc addr+0
                eor yc
                rts
                nop
                nop
                .endblock
                .cerror size(type2)!=8,''

types_end:

; 0000abcd
; 0a0b0c0d
                
pixel_value: .function colour
right=(colour&8)<<3|(colour&4)<<2|(colour&2)<<1|(colour&1)<<0
                .endfunction (right<<1|right<<0)^$ff
                
pixel_table:
                .for i=0,i<8,i+=1
                .if (i&4)!=0
                .byte pixel_value(7)
                .elsif (i&2)!=0
                .byte pixel_value(2)
                .elsif (i&1)!=0
                .byte pixel_value(1)
                .else
                .byte pixel_value(0)
                .endif
                .endfor

                .endlogical
                .endsection