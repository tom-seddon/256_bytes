                .include "submodules/beeb/include/beeb.s65"
                .cpu '65c02'

; refinement of lovebyte_2023_2
                
;-------------------------------------------------------------------------

*=$1900
                .dsection code

DMAX_COLOUR_CHANGE_COUNTER_VALUE=10
MIN_MAX_COLOUR=16
MAX_MAX_COLOUR=22
                
;-------------------------------------------------------------------------

sn_freq_reg: .function ch
                .cerror ch<0||ch>3,"invalid SN channel"
                .endfunction $80|ch<<5

sn_atten_reg: .function ch
                .cerror ch<0||ch>3,"invalid SN channel"
                .endfunction $80|ch<<5|$10
                
sn_atten: .function ch,atten
                .cerror ch<0||ch>3,"invalid SN channel"
                .cerror atten<0||atten>15,"invalid SN attenuation"
                .endfunction sn_atten_reg(ch)|atten

sn_freqh: .function freq
                .cerror freq<0||freq>1023,"invalid SN frequency"
                .endfunction freq>>4

sn_freql: .function ch,freq
                .cerror freq<0||freq>1023,"invalid SN frequency"
                .endfunction sn_freq_reg(ch)|freq&15
                
                .section code
start:
                lda #22
                jsr oswrch
                lda #2
                jsr oswrch
                sei
                tax
copy_zp_loop:
                lda zp_code-2,x
                sta $fe,x
                stx system_via.ddra
                inx
                bne copy_zp_loop
                jmp zp_code_actual_start

zp_code:
                .logical 0
dmax_colour: .byte 0
max_colour: .byte MIN_MAX_COLOUR
dmax_colour_change_counter: .byte DMAX_COLOUR_CHANGE_COUNTER_VALUE
zp_code_actual_start:
                ; lda #sn_atten(1,1)
                ; jsr sn_write

                ; ; silence startup beep on tone 3 
                ; lda #sn_atten(0,15)
                ; jsr sn_write

main_loop:
                ldy #19
blocks_loop:
colour=*+1;;;;;;;;;;;;;;;;;<--note
                lda #0;;;;;<--note
                inc a
                cmp max_colour
                bcc got_colour
                lda #0
got_colour:
                sta colour

                lda #0
                ldx colour
                cpx #pixels_end-pixels
                bcs fill
                lda pixels,x
fill:
                ldx #7
fill_loop:
dest=*+1;;;;;;;;;;;;;;;;;;;;;;<-- note
                sta $3000,x;;;<-- note
                dex
                bpl fill_loop

                lda #13
fe00_constant=*+1;;;;;;;;;;<--note
                sta $fe00;;<--note
fe01_constant=*+1;;;;;;;;;;<--note
                inc $fe01;;<--note
                bne got_crtc
                dec a
                sta (fe00_constant)
                lda (fe01_constant)
                inc a
                cmp #$10
                bcc got_crtc_msb
                lda #$06
got_crtc_msb:
                sta $fe01

got_crtc:

                clc
                lda dest+0
                adc #8
                sta dest+0
                bcc dest_updated

update_dmax_colour:                
                dec dmax_colour_change_counter
                bne update_dest_msb

                ; C=1
                lda max_colour
                adc dmax_colour ;0=+1/$fe=-1
                cmp #MAX_MAX_COLOUR+1
                bcc max_max_ok
max_max_breached:
                ldx #MAX_MAX_COLOUR
                bra toggle_dmax_colour
max_max_ok:
                cmp #MIN_MAX_COLOUR
                bcs min_max_ok
                ldx #MIN_MAX_COLOUR
toggle_dmax_colour:
                lda dmax_colour
                eor #$fe
                sta dmax_colour
                txa
min_max_ok:
                sta max_colour
                
                lda #DMAX_COLOUR_CHANGE_COUNTER_VALUE
                sta dmax_colour_change_counter

update_dest_msb:
                inc dest+1
                bpl dest_updated
                lda #$30
                sta dest+1

dest_updated:
                dey
                bne blocks_loop

                lda #2
wait_vsync_loop:
via_ifr_constant=*+1;;;;;;;;;;;;;;;;<--note
                bit system_via.ifr;;<--note
                beq wait_vsync_loop
                sta (via_ifr_constant)

                jmp main_loop

sn_write: .proc
                sta system_via.ora_no_handshake
                stz system_via.orb
                ldx #8
                jsr +
                jsr +
                stx system_via.orb
+
                rts
                .endproc

                
pixels:
                .byte $00       ;brk <---------- no
                .byte $20       ;jsr $1230
                .byte $30       ;bmi rel <------ must be untaken!
                .byte $12       ;ora $03
                .byte $03       ;1-byte nop
                .byte $23       ;1-byte nop
                .byte $33       ;1-byte nop
                .byte $19       ;ora $2c0c,y
                .byte $0c       ;tsb $3c2c
                .byte $2c       ;bit $1e3c
                .byte $3c       ;bit $0f1e,x
                .byte $1e       ;asl $2f0f,x <-- no
                .byte $0f       ;1-byte nop
                .byte $2f       ;1-byte nop
                .byte $3f       ;1-byte nop
pixels_end:

                .endlogical
                .endsection
                